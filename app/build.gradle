apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "com.gh0u1l5.wechatmagician"
        minSdkVersion 21
        maxSdkVersion 29
        targetSdkVersion 29
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += "src/androidTest/kotlin"
    }

    signingConfigs {
        if (System.getenv("CI")) {
            release {
                storeFile rootProject.file('release.keystore')
                storePassword System.getenv("RELEASE_KEYSTORE_PASS")
                keyAlias System.getenv("RELEASE_KEYSTORE_ALIAS_NAME")
                keyPassword System.getenv("RELEASE_KEYSTORE_ALIAS_PASS")
            }
        }
    }
}

android.applicationVariants.all { variant ->
    variant.registerJavaGeneratingTask(generatePluginList)
    variant.outputs.all {
        outputFileName = "${rootProject.name}-${variant.buildType.name}.apk"
    }
}

dependencies {
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //noinspection GradleDependency
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.fragment:fragment:1.2.5'
    implementation 'androidx.preference:preference:1.1.1'

    implementation project(':spellbook')
    implementation 'com.github.kittinunf.fuel:fuel-android:1.13.0'
    implementation 'me.leolin:ShortcutBadger:1.1.19'
}

task generatePluginList() {
    def PLUGIN_DIR = "src/main/kotlin/com/gh0u1l5/wechatmagician/backend/plugins/"
    def PLUGIN_LIST_FILE = "src/main/kotlin/com/gh0u1l5/wechatmagician/backend/WechatPlugins.kt"

    def plugins = fileTree(PLUGIN_DIR).collect { file ->
        file.name.substring(0, file.name.length() - 3)
    }.sort()
    file(PLUGIN_LIST_FILE).text = """package com.gh0u1l5.wechatmagician.backend

import com.gh0u1l5.wechatmagician.backend.plugins.*

/**
 * Dynamically generated by build.gradle
 */
val WechatPlugins = listOf(
${plugins.join(",\n")}
)
"""
}